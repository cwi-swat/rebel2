module hotel::Hotel

spec Key

spec Card
  first: Key,
  second: Key;
  
  init event create(fst: Key, snd: Key) 
    post: this.first' = fst, this.second' = snd;
  
  internal event equal(fst: Key, snd: Key)
    pre: this.first = fst, this.second = snd;
    
  internal event check(r: Room) 
    pre: r.check(this.second) || (r.check(this.first) && r.update(this.second));  
    
  states:
    (*) -> created: create;
    created -> created: equal, check;
  
spec Guest
  card: Card;
 
  internal event checkin(card: Card)
    post: this.card' = card; 
  
  event enterRoom(r: Room)
    pre: this.card.check(r);
      
  states:
    (*) -> checkedin: checkin;
    checkedin -> checkedin: enterRoom;

spec Room
  key: Key;
  
  init event create(initialKey: Key) 
    post: this.key' = initialKey; 
    
  internal event update(newKey: Key) 
    post: this.key' = newKey;  
  
  internal event check(key: Key) 
    pre: this.key = key; 
  
  states:
    (*) -> created: create;
    created -> created: update, check;
  
spec FrontDesk
  issued: set Key,
  guests: set Guest;
    
  init event initialize()
    post: this.issued' = {}, this.guests' = {};
    
  event checkin(fst: Key, snd: Key, c: Card, r: Room, g: Guest)
    pre: 
      g.checkin(c),
      r.check(fst),
      c.equal(fst,snd), 
      snd notin this.issued,   
      g notin this.guests;
    post: 
      this.issued' = this.issued + snd,
      this.guests' = this.guests + g;
  
  states:
    (*) -> open: initialize;
    open -> open: checkin;   