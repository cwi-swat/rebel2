module banking::Account
   
import banking::types::Money 
import banking::types::Date
import banking::types::IBAN
 
spec Account
  nr: IBAN,
  balance: Money,  
  openedOn: Date,
  closedOn: Date;
  
  init event create(nr: IBAN, initialDeposit: Money, openedOn: Date)
    pre: initialDeposit.amount > 0, initialDeposit.currency = Currency[EUR], openedOn.same(Date[NOW]);
    post: this.nr' = nr, this.balance' = initialDeposit, this.openedOn' = openedOn;
  
  event deposit(amount: Money)
    pre:amount.amount > 0, this.balance.plus(amount);  
    //post: this.balance.amount' = this.balance.amount + amount.amount;
  
  event withdraw(amount: Money) 
    pre: amount.amount > 0, this.balance.minIfGte(amount);
    
  event payInterest(rate: Integer)  
    pre: this.balance.addInterest(rate); 
    
  event blocked() 
  event unblocked()  
  
  event close(closedOn: Date)
    pre: this.balance.zero(), closedOn.same(Date[NOW]);
    post: this.closedOn' = closedOn;
  
  final event removed() 
    pre: this.closedOn.yearsAfter(Date[NOW], 5);
    
  fact AllAccountsHaveUniqueIBANs = always forall ac1,ac2: Account | (ac1 is initialized && ac2 is initialized && ac1.nr = ac2.nr => ac1 = ac2);
  
  states:  
    //(*) -> open: create;  
    (*) -> waitingForActivation: create;  
    waitingForActivation -> open: deposit;  
    open -> open: deposit, withdraw, payInterest; 
    open -> block: blocked;  
    block -> open: unblocked; 
    open -> archive: close;  
    archive -> (*): removed;  
  
config SomeAccount = ac: Account is uninitialized, iban1: IBAN, m1: Money, d1: Date;
 
assert CanBeOpened = eventually exists ac:Account | ac is open; 
assert CanDeposit = eventually exists ac:Account | deposit on ac;
assert CanWithdraw = eventually exists ac:Account | withdraw on ac;
assert CanBeRemoved = eventually exists ac:Account | ac is finalized;
  
assert CanBeOverdrawn = eventually exists ac:Account | ac.balance.amount < 0;  
  
check CanBeOpened from SomeAccount in max 2 steps;          
check CanDeposit from SomeAccount in max 3 steps;         
check CanWithdraw from SomeAccount in max 4 steps;      
check CanBeRemoved from SomeAccount in max 6 steps;    

check CanBeOverdrawn from SomeAccount in max 4 steps;