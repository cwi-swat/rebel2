module paper::performance::rebel::Hotel

spec Key

spec Card
  frst: Key,
  second: Key;
  
  event create(fst: Key, snd: Key) 
    pre: fst != snd;
    post: this.frst' = fst, this.second' = snd;
      
  states:
    (*) -> created: create;
  
spec Guest
  card: Card,
  room: Room;
 
  internal event checkin(card: Card, room: Room)
    pre: card is initialized, room is initialized, card.frst = room.key;
    post: this.card' = card, this.room' = room; 
  
  event enterRoom()   
    pre: this.room.enterRoom(this.card);  
      
  states:  
    (*) -> checkedin: checkin;
    checkedin -> checkedin: enterRoom;
 
spec Room  
  key: Key;  
  
  event create(initialKey: Key)    
    post: this.key' = initialKey; 
     
  internal event enterRoom(card: Card) 
    variant normal
      pre: this.key = card.second;
    
    variant update
      pre: this.key = card.frst;
      post: this.key' = card.second;
    
  states:
    createdd -> createdd: enterRoom::normal, enterRoom::update;
    (*) -> createdd: create;
       
  
spec FrontDesk
  issued: set Key;
    
  event initialize() 
    post: this.issued' = {};   
    
  event checkin(card: Card, room: Room, guest: Guest)
    pre:  guest.checkin(card, room),  
          //card.frst notin this.issued,
          card.second notin this.issued;
    post: this.issued' = this.issued + card.second;
    
  states:
    (*) -> open: initialize;
    open -> open: checkin;    
    
config EmptyHotel
  = k1,k2,k3: Key, 
    g1,g2: Guest is uninitialized, 
    ca1,ca2: Card is uninitialized, 
    r1: Room is uninitialized, 
    fd: FrontDesk is uninitialized;

assert CanEnterRoomTwice
  = eventually exists g:Guest | enterRoom on g && next enterRoom on g;
  
assert NoIntruder 
  = eventually exists g1,g2:Guest | 
      g1 != g2 && 
      enterRoom on g1 &&   
        next (enterRoom on g2 && 
          next enterRoom on g1);   
      
run NoIntruder from EmptyHotel in max 10 steps;
run CanEnterRoomTwice from EmptyHotel in max 10 steps;